base is silent
	( NEWLINE | IMPORT_IMAGE | IMPORT_TRAIT | item as item in items )*

#ITEMS
itemToken-s
	[[a-zA-Z_][a-zA-Z0-9_]*] as NAME
	"hp" as HP
	"def" as DEF
	"dmg" as DMG
	"value" as VAL
	"weight" as WEI
itemBrace-s are Braced
	[:,;] as ITEM_PARAMETERS with item_parameter*
item-s are empty
event_name-s
	[equip] as equip
	[unequip] as unequip
	[change_item] as change_item
	[pre_attack] as pre_attack
	[post_attack] as post_attack
	[tick] as tick
	[gained] as gained
	[lost] as lost
item is silent
	NAME as itemName ITEM_PARAMETERS
item_parameter is silent
	NEWLINE
	HP statRange as hp
	DMG statRange as attack
	DEF statRange as defense
	VAL statRange as value
	WEI statRange as weight
	( event_name animation? BODY ) as event
	( trait_name ( IF boolean_statement )? ) as trait

#TRAITS
trait_name-s are empty
traitToken-s
	[[a-zA-Z_][a-zA-Z0-9_]*] as NAME
	[.*] as WILD
	"\n" as NEWLINE
	"\n\t" as NEWTAB
	"trts" as TRTS
	"-ness" as TRAIT_NESS
traitBrace-s are Braced
	[,.] as TRAIT_FILE with WILD as trait_file_name 
	[\n\t,.] as TRAIT_DESCRIPTION with WILD as description
traitImport-s are Import
	[><<\"../traits/\",trait_file_name,\".trts\">>\n] as IMPORT_TRAIT from blank:(NEWLINE|trait_declaration)*; with TRAIT_FILE TRTS
trait_declaration
	NAME as traitName in trait_names TRAIT_DESCRIPTION (NEWLINE|(event_name animation? BODY)as event )*

#HEROES
hero_stat-s
	"STR" as STR
	"DEX" as DEX
	"CON" as CON
	"KNW" as KNW
	"MND" as MND
	"CRM" as CRM
statRange is silent
	range hero_stat?

#GENERAL
generalToken-s
	[[a-zA-Z_][a-zA-Z0-9_]*] as NAME
	[[-]?\\d*\\Q.\\E\\d+] as FLOAT
	[[-]?\\d+] as INTEGER
	[.*] as WILD
	[[<>][=]?] as ORDINAL
	":" as COLON
	";" as SEMICOLON
	".." as DOTDOT
	"->" as ACCESS
	"." as DOT
	"if" as IF
	"else" as ELSE
	"=" as EQUALSIGN
	"==" as EQUALS
	"!=" as NOTEQUALS
	"+" as PLUS
	"-" as MINUS
	"*" as MULTIPLY
	"/" as DIVIDE
	"&" as AND
	"|" as OR
	"!" as NOT
	"%" as PERCENT
	"\n" as NEWLINE
	"," as COMMA
	"-" as DASH
	"true" as TRUE
	"false" as FALSE
	"null" as NULL
	"chance" as CHANCE
	"gains" as GAINS
	"loses" as LOSES
	"from" as FROM
	"this" as THIS
	"on" as ON
	"impose" as IMPOSE
	"release" as RELEASE
	"has" as HAS
generalBrace-s are Braced
	[\",\"] as QUOTE with WILD as quote
	[{,}] as BODY with (NEWLINE | body_element)*
	[(,)] as METHOD_PARAMETERS with statement? ( COMMA NEWLINE* statement)*
	[(,)] as BRACED_STATEMENT with statement

generalAltBrace-s are CharitableBraced
	[,\n] as description with WILD as description

num is silent
	FLOAT
	INTEGER
range is silent
	num as left (DOTDOT|DASH) num as right PERCENT as percent?
operator
	PLUS | MINUS | DIVIDE | MULTIPLY | EQUALS | NOTEQUALS | ORDINAL

body_element
	if_statement
	stat_imposition
	trait_acquisition
	variable_declaration
	statement

trait_acquisition
	variable_call ( GAINS | LOSES ) as operator ( THIS | trait_name ) ( FROM NAME as source_name )?
stat_imposition
	variable_call ( IMPOSE as operator INTEGER as amount ON hero_stat | RELEASE as operator hero_stat ) 

if_statement
	IF boolean_statement NEWLINE* BODY ( NEWLINE* ELSE NEWLINE* ( BODY | if_statement) ) as else_statement?

boolean_statement
	(boolean_element ((AND|OR) as operator boolean_element )*
boolean_element is silent
	(num PERCENT CHANCE) as chance
	has_statement
	statement

has_statement
	NOT? variable_call HAS (trait_name TRAIT_NESS)

statement
	NOT? BRACED_STATEMENT as braced ( operator NEWLINE* statement )*
	NOT? variable_call ( operator NEWLINE* statement )*

variable_declaration
	NAME as type NAME as variableName EQUALSIGN statement

variable_call
	range as range ( ACCESS NAME as variableName | DOT method )*
	( TRUE | FALSE | NULL | num ) as exact
	( method | NAME as variableName | QUOTE )  ( ACCESS NAME as variableName | DOT method )*

method
	NAME as methodName METHOD_PARAMETERS

#IMAGES
image_name-s are empty
imageToken-s
	[[a-zA-Z_][a-zA-Z0-9_]*] as NAME
	[.*] as WILD
	"\n" as NEWLINE
	"," as COMMA
	[[Xx]] as CROSS
	"-" as DASH
	">" as LESSTHAN
	"imgs" as IMGS
imageBrace-s are Braced
	[,.] as IMAGE_FILE with WILD as image_file_name
imageImport-s are Import
	[><<image_file_name,\".imgs\">>\n] as IMPORT_IMAGE from blank:(NEWLINE|image_declaration)+; with (IMAGE_FILE IMGS)
image_declaration
	NAME as imageName in image_names ( INTEGER as left CROSS INTEGER as right? ) EQUALSIGN IMAGE_FILE
image_id
	image_name ( INTEGER as left (COMMA INTEGER as right)? )?
animation
	image_id ( DASH INTEGER as frames? LESSTHAN ( image_name| INTEGER as left (COMMA INTEGER as right)?)) as delta*